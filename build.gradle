buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'
    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 22
    }
	sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['resources']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
        androidTest {
            java.srcDirs = ['tests']
        }
    }
    packagingOptions {
        pickFirst "META-INF/DEPENDENCIES"
        pickFirst "META-INF/LICENSE"
        pickFirst "META-INF/NOTICE"
    }
    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),'proguard-rules.pro'
        }
    }
}

dependencies {
    repositories {
        mavenCentral()
    }
    compile 'com.android.support:support-v4:22.2.0'
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'org.apache.mina:mina-core:2.0.9'
    compile 'org.apache.sshd:sshd-core:0.8.0'
    compile 'org.bouncycastle:bcprov-jdk16:1.46'


    androidTestCompile 'org.mockito:mockito-core:1.10.19'

    // Because mockito has some problems with dex environment
    androidTestCompile 'com.google.dexmaker:dexmaker:1.1'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.1'

    compile 'org.atteo.classindex:classindex:3.3'

    //compile fileTree(dir: 'libs', include: '*.jar')
}

// Package the code generated by ClassIndex into the APK
project.android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def pkgTask = output.packageApplication
        def String variantName = variant.getName().capitalize()
        if (pkgTask.getName().indexOf(variantName) != -1) {
            pkgTask.setJavaResourceDir(new File(buildDir, "intermediates/classes/$variant.dirName"))
        }
    }
}
